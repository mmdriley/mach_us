/* 
 * Mach Operating System
 * Copyright (c) 1994,1993,1992,1991,1990,1989,1988 Carnegie Mellon University
 * All Rights Reserved.
 * 
 * Permission to use, copy, modify and distribute this software and its
 * documentation is hereby granted, provided that both the copyright
 * notice and this permission notice appear in all copies of the
 * software, derivative works or modified versions, and any portions
 * thereof, and that both notices appear in supporting documentation.
 * 
 * CARNEGIE MELLON ALLOWS FREE USE OF THIS SOFTWARE IN ITS "AS IS"
 * CONDITION.  CARNEGIE MELLON DISCLAIMS ANY LIABILITY OF ANY KIND FOR
 * ANY DAMAGES WHATSOEVER RESULTING FROM THE USE OF THIS SOFTWARE.
 * 
 * Carnegie Mellon requests users of this software to return to
 * 
 *  Software Distribution Coordinator   or   Software.Distribution@CS.CMU.EDU
 *  School of Computer Science
 *  Carnegie Mellon University
 *  Pittsburgh PA 15213-3890
 * 
 * any improvements or extensions that they made and grant Carnegie Mellon the
 * rights to redistribute these changes.
 */
/*
 * auth.defs
 *
 *	MIG definitions for Authentication Service.
 *
 */
/*
 * HISTORY:
 * $Log:	auth.defs,v $
 * Revision 2.4  94/07/08  18:13:41  mrt
 * 	Updated copyrights
 * 
 * Revision 2.3  94/06/01  18:19:44  mrt
 * 	Changed #include "" to <>
 * 	[94/04/20  14:15:53  mrt]
 * 
 * Revision 2.2  92/03/05  14:59:13  jms
 * 	Moved here from defs land.
 * 
 * Revision 1.3.1.1  92/02/26  16:36:53  jms
 * 	Translate to use latest MIG
 * 
 * Revision 1.3  90/12/19  11:04:04  jjc
 * 	Added as_create_token_group().
 * 	[90/12/05            jjc]
 * 	Added as_login_create().
 * 	[90/10/09            jjc]
 * 
 * Revision 1.2  89/05/17  15:48:19  dorr
 * 	add as_login_priv()
 * 
 * Revision 1.1.1.1  89/05/15  11:26:14  dorr
 * 	add as_login_priv()
 * 
 * 21-Oct-87  Robert Sansom (rds) at Carnegie Mellon University
 *	Added as_change_owner.  Added owner_id to translate routines.
 *
 * 30-May-87  Robert Sansom (rds) at Carnegie Mellon University
 *	Changes for the new mig.
 *
 *  9-Apr-87  Robert Sansom (rds) at Carnegie Mellon University
 *	Replaced as_verify_token by as_verify_token_ids and as_verify_token_names.
 *
 * 17-Feb-87  Robert Sansom (rds) at Carnegie Mellon University
 *	Started.
 *
 */

#include <auth_types.defs>

import "auth_defs.h";

subsystem auth 3300;

routine as_login(	server_port		: mach_port_t;
			user_name		: group_name_t;
			pass_word		: pass_word_t;
		out	private_port		: mach_port_make_send_t
);

routine as_login_priv(	server_port		: mach_port_t;
			private_port		: mach_port_t;
			user_id			: group_id_t;
		out	new_private_port	: mach_port_make_send_t);


routine as_logout(	server_port		: mach_port_t;
			private_port		: mach_port_t);


routine as_create_token(server_port		: mach_port_t;
			private_port		: mach_port_t;
			group_ids		: group_id_list_t;
		out	token			: mach_port_make_send_t);

routine as_delete_token(server_port		: mach_port_t;
			private_port		: mach_port_t;
			token			: mach_port_t);

routine as_verify_token_ids(server_port		: mach_port_t;
			token			: mach_port_t;
		out	user_id			: group_id_t;
		out	group_ids		: group_id_list_t);

routine as_verify_token_names(server_port	: mach_port_t;
			token			: mach_port_t;
		out	user_name		: group_name_t;
		out	group_names		: group_name_list_t);

routine as_create_group(server_port		: mach_port_t;
			private_port		: mach_port_t;
			group_name		: group_name_t;
			group_type		: group_type_t;
			full_name		: full_name_t;
			pass_word		: pass_word_t;
			owner_group		: group_name_t;
		inout	group_id		: group_id_t);

routine as_delete_group(server_port		: mach_port_t;
			private_port		: mach_port_t;
			group_name		: group_name_t);

routine as_change_password(server_port		: mach_port_t;
			private_port		: mach_port_t;
			user_name		: group_name_t;
			old_pass_word		: pass_word_t;
			new_pass_word		: pass_word_t);

routine as_change_owner(server_port		: mach_port_t;
			private_port		: mach_port_t;
			group_name		: group_name_t;
			new_owner		: group_name_t);

routine as_add_to_group(server_port		: mach_port_t;
			private_port		: mach_port_t;
			group_name		: group_name_t;
			add_group_name		: group_name_t);

routine as_remove_from_group(server_port	: mach_port_t;
			private_port		: mach_port_t;
			group_name		: group_name_t;
			remove_group_name	: group_name_t);

routine as_translate_group_id(server_port	: mach_port_t;
			group_id		: group_id_t;
		out	owner_id		: group_id_t;
		out	group_name		: group_name_t;
		out	group_type		: group_type_t;
		out	full_name		: full_name_t);

routine as_translate_group_name(server_port	: mach_port_t;
			group_name		: group_name_t;
		out	owner_id		: group_id_t;
		out	group_id		: group_id_t;
		out	group_type		: group_type_t;
		out	full_name		: full_name_t);

routine as_list_members(server_port		: mach_port_t;
			group_name		: group_name_t;
			trans_closure		: boolean_t;
		out	group_ids		: group_id_list_t =
							^ array [ ] of (MACH_MSG_TYPE_INTEGER_32, 32, dealloc));

routine as_list_memberships(server_port		: mach_port_t;
			group_name		: group_name_t;
			trans_closure		: boolean_t;
		out	group_ids		: group_id_list_t =
							^ array [ ] of (MACH_MSG_TYPE_INTEGER_32, 32, dealloc));

routine as_list_all_groups(server_port		: mach_port_t;
		out	group_ids		: group_id_list_t =
							^ array [ ] of (MACH_MSG_TYPE_INTEGER_32, 32, dealloc));


routine as_login_create(server_port		: mach_port_t;
			private_port		: mach_port_t;
			id			: group_id_list_t;
		out	new_private_port	: mach_port_make_send_t);

routine as_create_token_group(server_port	: mach_port_t;
			private_port		: mach_port_t;
			group_ids		: group_id_list_t;
		out	token			: mach_port_make_send_t);
